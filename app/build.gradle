import java.nio.file.Paths

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def sdk = 31

android {
    compileSdk sdk
    buildToolsVersion '32.0.0'

    defaultConfig {
        applicationId 'me.gm.cleaner.plugin'
        minSdk 29
        targetSdk sdk
        versionCode gitCommitCount
        versionName 'alpha3'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled false
        resConfigs 'en', 'zh-rCN', 'zh-rTW'
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Media Provider Manager_${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/**', '/kotlin/**', '/okhttp3/**']
        }
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        checkDependencies true
    }
}

def optimizeReleaseRes() {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates', 'optimized_processed_res', 'release', "resources-release-optimize.ap_")
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize',
                '--collapse-resource-names',
                '--enable-sparse-encoding',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

afterEvaluate {
    optimizeReleaseResources.doLast {
        optimizeReleaseRes()
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"
    def lifecycleVersion = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    def roomVersion = '2.4.0-beta02'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    def kotlinxCoroutinesVersion = '1.6.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinxCoroutinesVersion"
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"
    implementation 'androidx.paging:paging-runtime-ktx:3.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.16.1'

    implementation 'com.google.code.gson:gson:2.8.9'
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    def appCenterSdkVersion = '4.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:$appCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-crashes:$appCenterSdkVersion"

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.2'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
    implementation 'me.zhanghai.android.appiconloader:appiconloader-glide:1.3.1'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.1'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:html:$markwonVersion"
    implementation "io.noties.markwon:image:$markwonVersion"
    implementation 'com.caverock:androidsvg:1.4'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.20'
}
