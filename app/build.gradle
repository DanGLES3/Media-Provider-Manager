import org.eclipse.jgit.api.Git
import org.eclipse.jgit.internal.storage.file.FileRepository

import java.nio.file.Paths

apply plugin: 'com.android.application'

def repo = new FileRepository(rootProject.file(".git"))
def refId = repo.refDatabase.exactRef("refs/remotes/origin/main").objectId
def gitCommitCount = new Git(repo).log().add(refId).call().size()

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'me.gm.cleaner.xposed'
        minSdkVersion 24
        targetSdkVersion 31
        versionCode gitCommitCount
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled false
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Media Provider Hooker_${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude '/META-INF/**'
        exclude '/kotlin/**'
        exclude '/okhttp3/**'
    }
}

def optimizeReleaseResources = task('optimizeReleaseResources').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates',
            'processed_res', 'release', 'out', "resources-release.ap_")
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '--shorten-resource-paths',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'processReleaseResources') {
        task.finalizedBy optimizeReleaseResources
    }
}

dependencies {
    compileOnly files('libs/XposedBridgeApi-89.jar')
    api 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
}
